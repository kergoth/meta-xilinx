#@TYPE: Machine
#@NAME: zcu102-zynqmp-mel
#@DESCRIPTION: Mentor Embedded Linux Machine support for zcu102 MPSoc Evaluation Board.
#
# For full details on the ZCU102 and associated tools, refer to the meta-xilinx
# layer.

require conf/machine/zcu102-zynqmp.conf

#mask buggy bbappends
BBMASK += "/recipes-zynqmp/qemu/qemu_2.5%.bbappend"
BBMASK += "/recipes-bsp/u-boot/u-boot-xlnx_2015.04.bbappend"
BBMASK += "/recipes-multimedia/gstreamer/gstreamer1.0_1.6.3.bbappend"

PREFERRED_PROVIDER_virtual/bootloader = "u-boot-xlnx-dev"

PREFERRED_PROVIDER_virtual/libgles1 = "libmali-xlnx"
PREFERRED_PROVIDER_virtual/libgles2 = "libmali-xlnx"
PREFERRED_PROVIDER_virtual/egl = "libmali-xlnx"

XSERVER = " \
           xserver-xorg \
           xf86-input-evdev \
           xf86-input-mouse \
           xf86-input-keyboard \
           ${@bb.utils.contains('DISTRO_FEATURES', 'opengl', 'xserver-xorg-extension-glx', '', d)} \
           "
XSERVER_append = " xf86-video-armsoc"

# WIC image type support
IMAGE_FSTYPES += "wic.bz2"
WKS_FILE ?= "sdimage-bootpart-4g.wks"

# dosfstools is needed to create the boot partition, mtools to copy to it
IMAGE_DEPENDS_wic_append = " dosfstools-native mtools-native parted-native"
IMAGE_BOOT_FILES += " boot.bin atf.ub uEnv.txt system.dtb"

# u-boot is needed for the IMAGE_BOOT_FILES
do_image_wic[depends] += "virtual/bootloader:do_deploy"

MACHINE_EXTRA_RDEPENDS += "mtd-utils"
MACHINE_EXTRA_RRECOMMENDS += "udev-extraconf kernel-modules firmware-wireless"

# Set 128KB eraseblock size for on-board NOR FLASH
JFFS2_ERASEBLOCK ?="0x20000"

# kernel modules for ZynqMP
MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS_append_zynqmp = " kernel-module-mali"

# Metadata used by CodeBench for the ADE
INHERIT += "${@'cb-mbs-options' if 'mentor-private' in '${BBFILE_COLLECTIONS}'.split() else ''}"
ADE_CB_MBS[general.cpu] = "general.cpu.armv8a"
